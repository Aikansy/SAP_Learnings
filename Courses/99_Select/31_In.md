# **PROGRAMMATION**

## **IN VALUE**

Vérifie si la valeur de la colonne fait partie (**IN**) ou non (**NOT IN**) des valeurs entre parenthèses.

_Exemple_

Recherche du nom et du prénom des passagers n’habitant pas les villes de Toulouse et Barcelone.

```ABAP
SELECT surname,
       name
  FROM zpassenger
  INTO TABLE @DATA(lt_passenger)
  WHERE city NOT IN ('TOULOUSE', 'BARCELONE').


DATA ls_passenger LIKE LINE OF lt_passenger.

LOOP AT lt_passenger INTO ls_passenger.
  WRITE:/ ls_passenger-surname, ls_passenger-name.
ENDLOOP.
```

| **SURNAME** | **NAME** |
| ----------- | -------- |
| PEREZ       | MICHEL   |

## **IN SELTAB**

Pour cette fonction, le paramètre **SELTAB** signifie selection table (table de sélection), et est le plus souvent associé à un champ d’un **écran de sélection** de type **SELECT-OPTIONS** (pour options de sélection dont la notion sera détaillée dans le chapitre Création d’un programme **ABAP** - Écran de sélection). La particularité de ce type de champ est qu’il est en fait, une table interne de type **Range** (cf. chapitre Les tables internes - Caractéristiques).

L’exemple suivant va reprendre celui créé avec l’option **BETWEEN** : sélectionner toutes les marques et modèles de voitures dont l’année est comprise entre 2013 et 2016, plus ceux de 2011. Pour cela, une table interne de type **RANGE** avec comme référence l’élément de données **VLC_YEAR_OF_CONSTRUCTION**, sera créée et renseignée avec toutes les options de date, afin d’être utilisée dans la clause **WHERE**.

```ABAP
DATA: lr_year TYPE RANGE OF vlc_year_of_construction.
DATA: ls_year LIKE LINE OF r_year.

ls_year-sign   = 'I'.
ls_year-option = 'BT'.
ls_year-low    = '2013'.
ls_year-high   = '2016'.
APPEND ls_year TO lr_year.

CLEAR ls_year.
ls_year-sign   = 'I'.
ls_year-option = 'EQ'.
ls_year-low    = '2011'.
APPEND ls_year TO lr_year.

SELECT brand,
       model
  FROM zcar_brand_mod
  INTO TABLE @DATA(lt_car)
  WHERE model_year IN @lr_year.

DATA ls_car LIKE LINE OF lt_car.

LOOP AT lt_car INTO ls_car.
  WRITE:/ ls_car-brand, ls_car-model.
ENDLOOP.
```

| **BRAND**  | **MODEL** |
| ---------- | --------- |
| AUDI       | A3        |
| FORD       | ESCORT    |
| PEUGEOT    | 307       |
| RENAULT    | MEGANE    |
| VOLKSWAGEN | GOLF      |
